buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
       classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

apply plugin: 'idea'
apply plugin: 'forge'
apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'maven-publish'

ext.buildProps = file "build.properties"
 
buildProps.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}
 
group = "minechem"
 
minecraft {
    version = "${rootProject.config.version.minecraft}-${rootProject.config.version.forge}"
    
    replaceIn "ModMinechem.java"
    replace "@MAJOR@", rootProject.config.version.mod.major + "." + rootProject.config.version.mod.minor
    replace "@BUILD@", "${System.getenv().BUILD_NUMBER}"
}

allprojects {
    version = "${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"
    
    if (System.getenv("BUILD_NUMBER") != null)
        version += ".${System.getenv("BUILD_NUMBER")}"
        
	repositories {
        maven {
            name 'Calclavia Maven'
            url 'http://calclavia.com/maven'
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		ivy {
			name 'FMP'
			artifactPattern "http://files.minecraftforge.net/[module]/[module]-dev-[revision].[ext]"
		}
		ivy {
			artifactPattern "http://www.chickenbones.craftsaddle.org/Files/New_Versions/1.6.4/[module]-dev%20[revision].[ext]"
		}
        mavenCentral()
    }
    
    dependencies {
        compile group: 'universalelectricity', name: 'Universal-Electricity', version: "${rootProject.config.version.universalelectricity}", classifier: "dev"

		compile name: 'CodeChickenLib', version: "${config.version.minecraft}-${config.version.cclib}", ext: 'jar'
		compile name: 'NotEnoughItems', version: "${config.version.nei}", ext: 'jar'
		compile name: 'CodeChickenCore', version: "${config.version.cccore}", ext: 'jar'
	}
    
    jar {
		
        dependsOn ":copyBuildXml"
        destinationDir = file (rootProject.getRootDir().getPath() + '/output')
    }
	
	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifact jar
			}
		}
		repositories {
			maven {
				url "file://var/www/maven"
			}
		}
	}
}

processResources {
	from 'build.properties'
}
 
task copyBuildXml(type: Copy) {
    from 'build.properties'
    into 'output'
}
 
task apiZip(type: Zip) {
    classifier = 'api'
    from sourceSets*.allSource
    include 'minechem/api/**'
    destinationDir = file 'output'
}
	
artifacts {
    archives apiZip
}

build.dependsOn "apiZip", "copyBuildXml"
