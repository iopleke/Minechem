buildscript {
    repositories {
        mavenCentral()
		jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            // Add maven repo for SpecialSource
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
		classpath 'de.undercouch:gradle-download-task:1.2'
		classpath "org.scala-lang:scala-library:2.11.1"
    }
}

apply plugin: 'scala'
apply plugin: 'forge'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'de.undercouch.download'

ext.buildProps = file "build.properties"

buildProps.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

repositories {
    maven {
	name = "forge"
	url = "http://files.minecraftforge.net/maven"
    }
    maven { // the repo from which to get NEI and stuff
	name 'CB Repo'
	url "http://chickenbones.net/maven"
    }
    ivy {
	name 'FMP'
	artifactPattern "http://files.minecraftforge.net/[module]/[module]-dev-[revision].[ext]"
    }
}

dependencies {
    compile group: 'codechicken', name: 'NotEnoughItems', version: "${config.version.minecraft}-${config.version.nei}", ext: 'jar', classifier: "dev"
    compile group: 'codechicken', name: 'CodeChickenCore', version: "${config.version.minecraft}-${config.version.cccore}", ext: 'jar', classifier: "dev"
    compile group: 'codechicken', name: 'CodeChickenLib', version: "${config.version.minecraft}-${config.version.cclib}", ext: 'jar', classifier: "dev"
    compile fileTree(dir: 'lib', include: '*.jar')
    compile fileTree(dir: 'lib', include: '*.zip')
}

group = "minechem"
archivesBaseName = "Minechem"

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}"

    replaceIn "reference/Reference.java"
    replace "@MAJOR@", config.version.mod.major
    replace "@MINOR@", config.version.mod.minor
    replace "@REVIS@", config.version.mod.revis

    if (System.getenv("BUILD_NUMBER") != null) {
        replace "@BUILD@", System.getenv("BUILD_NUMBER")
    }
    else
    {
        replace "@BUILD@", "ManuallyBuilt"
    }
}

processResources {
    from 'build.properties'
}

allprojects {
    version = "${config.version.minecraft}-${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"

    if (System.getenv("BUILD_NUMBER") != null) {
        version += ".${System.getenv("BUILD_NUMBER")}"
    } else {
        version += "-ManuallyBuilt"
    }

    jar {
        dependsOn ":copyBuildXml"
        destinationDir = file (rootProject.getRootDir().getPath() + '/output')
		manifest {
			attributes 'FMLAT': 'minechem_at.cfg'
		}
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact jar
            }
        }
        repositories {
            maven {
                url "file://var/www/maven"
            }
        }
    }
}

task copyBuildXml(type: Copy) {
    from 'build.properties'
    into 'output'
}

task apiZip(type: Zip) {
    classifier = 'api'
    from sourceSets*.allSource
    include 'minechem/api/**'
    destinationDir = file 'output'
}

import de.undercouch.gradle.tasks.download.Download

task getLibraries {
	def libsFolder = new File('lib')
	if (!libsFolder.isDirectory() ) {
		libsFolder.mkdirs()
	}

	def ccSource = 'http://minecraft.curseforge.com/mc-mods/67504-computercraft/files/2216236/download'
	def ccDest = new File('lib', 'ComputerCraft1.65.jar')


	if(!ccDest.exists() ){
		download {
			src ccSource
			dest ccDest
		}
	}

	def mtSource = 'http://minetweaker3.powerofbytes.com/download/MineTweaker3-1.7.10-3.0.9B.jar'
	def mtDest = new File('lib', 'MineTweaker3-Dev-1.7.10-3.0.9.jar')

	if(!mtDest.exists() ){
		download {
			src mtSource
			dest mtDest
		}
	}

        def cofhSource = 'http://minecraft.curseforge.com/mc-mods/220333-cofhlib/files/2225609/download'
        def cofhDest = new File('lib', 'CoFHLib-1.7.10-1.0.0B9-68-dev.jar')

        if(!cofhDest.exists() ){
            download {
                src cofhSource
                dest cofhDest
            }
        }
}

artifacts {
    archives apiZip
}

build.dependsOn "apiZip", "copyBuildXml", "getLibraries"
